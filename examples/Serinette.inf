
! Serinette - Basic example of playing music and sound effects.

! The serinette (a type of music box) plays music when it opens and a sound effect when it's wound. We'll also show its picture when it's examined.
! The example media files can be downloaded from http://vorple-if.com/doc/inform7/examples .

! You should have one folder for the audio and one folder for the images, and change the "vorple.options.resource_paths" in play.html to point to the right folders.

Constant Story "Serinette^";

                       Include "vorple.h";
Include "parser";
Include "verblib";
                       Include "vorple-multimedia.h";
Include "grammar";



[ Initialise ;
	location = drawingroom;
	VorpleInitialise();
	VorplePreloadImage("serinette.jpg");
];


Attribute wound;


Object drawingroom "Drawing room"
	with description "The drawing room is tastefully decorated.",
has light;

[ PlayTune ;
	VorplePlayMusic("musicbox.mp3");
	give serinette ~wound;
];

Object serinette "serinette" drawingroom
	with description [; VorpleImage("serinette.jpg", "A music box", IMAGE_CENTERED); "There's a winding key behind the box."; ],
	describe "A beautiful music box sits on a table.",
	name 'serinette' 'music' 'box',
	before [; Wind: give self wound; VorplePlaySoundEffect("winding.mp3"); if (self has open) {PlayTune();} "You turn the winding key until it turns no more.";
		Receive: "It's not the kind of box that can contain anything other than its own mechanism.";
		Open: if (self has wound) {PlayTune(); } else {"Nothing happens. Looks like it must be wound first.";}
		Close: VorpleStopMusic();
		],
has openable container transparent;

Object windingkey "winding key" serinette
	with name 'winding' 'key',
	before [; Wind: <<Wind serinette>>;
	];


[ WindSub;
	if (noun has wound) {print (The) noun, " is already wound.";}
	else {"That's not something you can wind";}
];

Verb 'wind'
* noun		->Wind;